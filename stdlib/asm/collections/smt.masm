# Constant value for the depth at which leaves sit
const.LEAF_DEPTH=64

# SET
# =================================================================================================

#! Inserts or removes a value associated with the given key. The leaf to which we're inserting is
#! guaranteed to be empty.
#!
#! Inputs:  
#!   Operand stack: [V, K, R, ...]
#!
#! Outputs: 
#!   Operand stack: [V_old, R_new, ...]
#!
#! Cycles
#!   Insert empty value: 43 cycles
#!   Insert non-empty value: 102 cycles
proc.set_empty_leaf
    # Check if we're inserting the empty value (19 cycles)
    padw eqw
    #=> [V == ZERO, ZERO, V, K, R]

    if.true
        # Inserting an empty value; this is a no-op (4 cycles)
        dropw
        #=> [V (=ZERO), K, R, ...]

        # Prepare stack: verify that the leaf is actually empty
        # (9 cycles)
        movupw.2 swapw dup.8 movdn.4 push.LEAF_DEPTH movdn.4
        #=> [V (=ZERO), depth, K[3], R, K, ...]

        # (1 cycle)
        mtree_verify
        #=> [V (=ZERO), depth, K[3], R, K, ...]
        
        # Prepare stack for return (10 cycles)
        movup.4 drop movup.4 drop movupw.2 dropw
        #=> [V (=ZERO), R, ...]
    else
        # Inserting a non-empty value (4 cycles)
        dropw
        #=> [V, K, R, ...]

        # Update advice map
        adv.insert_hdword
        #=> [V, K, R, ...]

        # Compute hash([K, V]); the new node value (NV)
        # (21 cycles)
        dupw.1 swapw hmerge
        # => [NV, K, R]

        # Prepare stack for `mtree_set` (5 cycles)
        movupw.2 dup.8 push.LEAF_DEPTH
        #=> [depth, K[3], R, NV, K]

        # Insert node in Merkle store (29 cycles)
        mtree_set
        #=> [V_in_leaf, R_new, K]

        # Check that V_in_leaf is indeed empty (15 cycles)
        padw assert_eqw
        #=> [R_new, K]

        # Prepare stack for return (9 cycles)
        swapw dropw padw
        #=> [ZERO, R_new]
    end
end

#! Inserts a value at the given key. The leaf to which we're inserting is
#! guaranteed to hold a single key-value pair (provided on the advice stack).
#!
#! Inputs: 
#!   Operand stack: [V, V_old, K, R, ...]
#!   Advice stack:  [K_in_leaf, V_in_leaf]
#!
#! Outputs:
#!   Operand stack: [V_old, R_new, ...]
#!
#! Cycles:
#!   Leaf single after insertion: 121 cycles
#!   Leaf multiple after insertion: unimplemented
proc.insert_single_leaf
    # (1 cycle)
    swapw
    # => [V_old, V, K, R, ...] 

    # Note: We don't need the `V_in_leaf` value.
    # It is sufficient to deal with `V_old` from `adv.push_smtpeek`, and check its authenticity
    # at the end after inserting with `mtree_set`
    # (4 cycles)
    adv_push.8 dropw
    # => [K_in_leaf, V_old, V, K, R]

    # Check if the key stored in the leaf is the same
    # (15 cycles)
    dupw.3 eqw
    # => [K_in_leaf==K, K, K_in_leaf, V_old, V, K, R]

    if.true
        # Leaf stays a "single" variant

        # (8 cycles)
        dropw dropw
        # => [V_old, V, K, R]

        # Update advice map (3 cycles)
        movdnw.3 adv.insert_hdword
        # => [V, K, R, V_old]

        # Compute hash([K, V]); the new node value (NV)
        # (21 cycles)
        dupw.1 swapw hmerge
        # => [NV, K, R, V_old]

        # Prepare stack to update Merkle store
        # (6 cycles)
        movupw.2 dup.8 push.LEAF_DEPTH
        # => [depth, K[3], R, NV, K, V_old]

        # Update Merkle store (29 cycles)
        mtree_set
        # => [NV_old, R_new, K, V_old_from_peek]

        # Confirm that claimed `V_old` from `adv.push_smtpeek` is correct by checking if 
        # `[K, V_old]` hashes to `NV_old`
        # (33 cycles)
        movupw.2 dupw.3 hmerge assert_eqw
        # => [R_new, V_old_from_peek]

        # Clean up stack for return
        # (1 cycle)
        swapw
        # => [V_old, R_new]
    else
        # Leaf becomes a Multiple kv-pair case
        # TODO (fail for now)
        push.1 assertz
    end
end

#! Removes the provided key/value pair from the leaf. The leaf to which we're inserting is
#! guaranteed to hold a single key-value pair (provided on the advice stack). Hence, after the
#! operation, the leaf will be empty.
#!
#! Inputs:
#!   Operand stack: [V (=ZERO), V_old, K, R, ...]
#!
#! Outputs:
#!   Operand stack: [V_old, R_new, ...]
#!
#! Cycles: 143 
proc.remove_single_leaf
    # Update advice map
    # (3 cycles)
    movupw.2 swapw adv.insert_hdword
    # => [V, K, V_old, R, ...] 

    # Prepare the stack for `mtree_set`
    # Note that the new node value will be the empty word, so we can use `V`
    # as the node value (since we confirmed that it's `ZERO`)
    # (7 cycles)
    movupw.3 dup.8 push.LEAF_DEPTH
    # => [depth, K[3], R, V, K, V_old, ...] 

    # (29 cycles)
    mtree_set
    # => [NV_old, R_new, K, V_old, ...] 

    # Confirm that hmerge([K, V_old]) = NV_old
    # (33 cycles)
    movupw.2 dupw.3 hmerge assert_eqw
    # => [R_new, V_old, ...] 

    # Cleanup stack for return (1 cycle)
    swapw
    # => [V_old, R_new, ...] 
end

#! Inserts or removes a value associated with the given key. The leaf to which we're inserting is
#! guaranteed to hold a single key-value pair (provided on the advice stack).
#!
#! Inputs:
#!   Operand stack: [V, K, R, ...]
#!   Advice stack:  [K_in_leaf, V_in_leaf]
#!
#! Outputs:
#!   Operand stack: [V_old, R_new, ...]
#! Cycles:
#!   Remove: 168 cycles
#!   Insert; leaf single after insertion: 146 cycles
#!   Insert; leaf multiple after insertion: unimplemented
proc.set_single_leaf
    # (4 cycles)
    movdnw.2 adv.push_smtpeek movupw.2
    # => [V, K, R]

    # Push peeked value on stack (0 cycles)
    adv_push.4
    # => [V_old, V, K, R]

    # Check if we're inserting or removing a value
    # (21 cycles)
    swapw padw eqw
    # => [V==ZERO, ZERO, V, V_old, K, R, ...] 
    if.true
        # we're removing the key/value pair from the leaf; i.e. the leaf becomes empty

        # (4 cycles)
        dropw 
        # => [V, V_old, K, R, ...] 

        # (143 cycles)
        exec.remove_single_leaf
        # => [V_old, R_new]
    else
        # we're inserting the key/value pair

        # (4 cycles)
        dropw 
        # => [V, V_old, K, R, ...] 

        # (121 cycles)
        exec.insert_single_leaf
        # => [V_old, R_new]
    end
end

#! Inserts the specified value under the specified key in a Sparse Merkle Tree defined by the
#! specified root. If the insert is successful, the old value located under the specified key
#! is returned via the stack.
#!
#! If the VALUE is an empty word (i.e., [ZERO; 4]), the new state of the tree is guaranteed to
#! be equivalent to the state as if the updated value was never inserted.
#!
#! Inputs:
#!   Operand stack: [V, K, R, ...]
#! Outputs:
#!   Operand stack: [V_old, R_new, ...]
#!
#! Fails if the tree with the specified root does not exits in the VM's advice provider.
#!
#! Cycles
#!   Leaf empty
#!     removal: 74 cycles
#!     insertion: 133 cycles
#!   Leaf single
#!     removal: 227 cycles
#!     insertion (leaf remains single): 205
#!     insertion (leaf becomes multiple): unimplemented
#!   Leaf multiple
#!     unimplemented
export.set
    # Prepare stack for adv.push_mtnode
    movupw.2 dup.8 push.LEAF_DEPTH
    # => [depth, leaf_index, R, V, K]

    # Push MT node on advice stack, cleanup operand stack, and then
    # push MT node on operand stack (NV)
    adv.push_mtnode drop drop movdnw.2 adv_push.4
    # => [NV, V, K, R]

    # (19 cycles)
    padw eqw
    # => [NV == ZERO, ZERO, NV, V, K, R]

    if.true
        # empty leaf

        # (8 cycles)
        dropw dropw
        #=> [V, K, R]

        # (insert empty value: 43 cycles)
        # (insert non-empty value: 102 cycles)
        exec.set_empty_leaf
    else
        # Single or Multiple leaf

        # (4 cycles)
        dropw
        # => [NV, V, K, R]

        # Retrieve leaf pre-image on advice stack, and push leaf size on stack
        # Note: the rest of the leaf pre-image will be pulled out later
        # (4 cycles)
        adv.push_mapvaln dropw adv_push.1
        # => [leaf_size, V, K, R]

        # Leaf size will be a multiple of 8 (each kv-pair in a leaf is 8 elements)
        # (3 cycles)
        dup eq.8
        # => [is_single_kv_pair, leaf_size, V, K, R]

        if.true
            #  Single kv-pair case

            # (1 cycle)
            drop
            # => [V, K, R]

            # (remove key/value: 168 cycles)
            # (insert; leaf single after insertion: 146 cycles)
            exec.set_single_leaf
        else
            # Multiple kv-pair case
            # TODO (fail for now)
            push.1 assertz
        end
    end
end

# GET
# =================================================================================================

#! Returns the value located under the specified key in the Sparse Merkle Tree defined by the
#! specified root.
#!
#! If no values had been previously inserted under the specified key, an empty word (i.e.,
#! [ZERO; 4]) is returned.
#!
#! Inputs:
#!   Operand stack: [K, R, ...]
#!
#! Outputs:
#!   Operand stack: [V, R, ...]
#!
#! Fails if the tree with the specified root does not exits in the VM's advice provider.
#!
#! Cycles
#!   Leaf empty: 48 cycles
#!   Leaf single: 99 cycles
#!   Leaf multiple: unimplemented
export.get
    # Prepare for `mtree_get`
    # (6 cycles)
    dupw.1 dup.4 push.LEAF_DEPTH
    # => [depth, K[3], R, K, R]

    # Retrieve node value from merkle store
    # (14 cycles)
    mtree_get swapw dropw
    # => [NV, K, R]

    # Check if value is empty; if so, return empty value
    # (19 cycles)
    padw eqw
    # => [NV == 0, ZERO, V, K, R]

    if.true
        # Return empty value
        # (9 cycles)
        dropw swapw dropw
        # => [NV, R]
    else
        # Drop extra ZERO word
        # (4 cycles)
        dropw
        # => [NV, K, R]

        # Get leaf pre-image from advice map. Push the leaf preimage size on the stack
        # (0 cycles)
        adv.push_mapvaln adv_push.1
        # => [leaf_size, NV, K, R]

        # Leaf size will be a multiple of 8 (each kv-pair in a leaf is 8 elements)
        # (3 cycles)
        dup eq.8
        # => [is_single_kv_pair, leaf_size, NV, K, R]

        if.true
            # Single kv-pair case

            # Push leaf pre-image on stack (single K-V pair)
            # (1 cycle)
            drop adv_push.8
            # => [V, K, NV, K, R]

            # Confirm that the key stored in the leaf is as expected
            # (18 cycles)
            movupw.3 dupw.2 assert_eqw
            # => [V, K, NV, R]

            # Duplicate V to return it after hash check
            # (7 cycles)
            dupw movdnw.3
            # => [V, K, NV, V, R]

            # Hash leaf preimage and ensure that it equals node value
            # (27 cycles)
            hmerge assert_eqw
            # => [V, R]
        else
            # Multiple kv-pair case
            # TODO (fail for now)
            push.1 assertz
        end
    end
end
