# ===== ADDITION ================================================================================ #

# Performs addition of two unsigned 64 bit integers discarding the overflow. #
# The input values are assumed to be represented using 32 bit limbs, but this is not checked. #
# Stack transition looks as follows: #
# [b_hi, b_lo, a_hi, a_lo, ...] -> [c_hi, c_lo, ...], where c = (a + b) % 2^64 #
export.add_unsafe
    swap
    movup.3
    u32add.unsafe
    movup.3
    movup.3
    u32addc
    drop
end

# ===== MULTIPLICATION ========================================================================== #

# Performs multiplication of two unsigned 64 bit integers discarding the overflow. #
# The input values are assumed to be represented using 32 bit limbs, but this is not checked. #
# Stack transition looks as follows: #
# [b_hi, b_lo, a_hi, a_lo, ...] -> [c_hi, c_lo, ...], where c = (a * b) % 2^64 #
export.mul_unsafe
    dup.3
    dup.2
    u32mul.unsafe
    movup.4
    movup.4
    u32madd
    drop
    movup.3
    movup.3
    u32madd
    drop
end

# ===== COMPARISONS ============================================================================= #

# Performs less-than comparison of two unsigned 64 bit integers. #
# The input values are assumed to be represented using 32 bit limbs, but this is not checked. #
# Stack transition looks as follows: #
# [b_hi, b_lo, a_hi, a_lo, ...] -> c, ...], where c = 1 when a < b, and 0 otherwise. #
export.lt_unsafe
    movup.3
    movup.2
    u32sub.unsafe
    movdn.3
    drop
    u32sub.unsafe
    swap
    eq.0
    movup.2
    and
    or
end

# Performs greater-than comparison of two unsigned 64 bit integers. #
# The input values are assumed to be represented using 32 bit limbs, but this is not checked. #
# Stack transition looks as follows: #
# [b_hi, b_lo, a_hi, a_lo, ...] -> c, ...], where c = 1 when a > b, and 0 otherwise. #
export.gt_unsafe
    movup.2
    u32sub.unsafe
    movup.2
    movup.3
    u32sub.unsafe
    swap
    drop
    movup.2
    eq.0
    and
    or
end

# Performs less-than-or-equal comparison of two unsigned 64 bit integers. #
# The input values are assumed to be represented using 32 bit limbs, but this is not checked. #
# Stack transition looks as follows: #
# [b_hi, b_lo, a_hi, a_lo, ...] -> c, ...], where c = 1 when a <= b, and 0 otherwise. #
export.lte_unsafe
    exec.gt_unsafe
    not
end

# Performs greater-than-or-equal comparison of two unsigned 64 bit integers. #
# The input values are assumed to be represented using 32 bit limbs, but this is not checked. #
# Stack transition looks as follows: #
# [b_hi, b_lo, a_hi, a_lo, ...] -> c, ...], where c = 1 when a >= b, and 0 otherwise. #
export.gte_unsafe
    exec.lt_unsafe
    not
end

# ===== DIVISION ================================================================================ #

# Performs division of two unsigned 64 bit integers discarding the remainder. #
# The input values are assumed to be represented using 32 bit limbs, but this is not checked. #
# Stack transition looks as follows: #
# [b_hi, b_lo, a_hi, a_lo, ...] -> [c_hi, c_lo, ...], where c = a // b #
export.div_unsafe
    adv.u64div          # inject the quotient and the remainder into the advice tape #
    
    push.adv.1          # read the quotient from the advice tape and make sure it consists of #
    u32assert           # 32-bit limbs #
    push.adv.1          # TODO: this can be optimized once we have u32assert2 instruction #
    u32assert

    dup.3               # multiply quotient by the divisor and make sure the resulting value #
    dup.2               # fits into 2 32-bit limbs #
    u32mul.unsafe
    dup.4
    dup.4
    u32madd
    eq.0
    assert
    dup.5
    dup.3
    u32madd
    eq.0
    assert
    dup.4
    dup.3
    mul
    eq.0
    assert

    push.adv.1          # read the remainder from the advice tape and make sure it consists of #
    u32assert           # 32-bit limbs #
    push.adv.1
    u32assert

    movup.7             # make sure the divisor is greater than the remainder. this also consumes #
    movup.7             # the divisor #
    dup.3
    dup.3
    exec.gt_unsafe
    assert

    swap                # add remainder to the previous result; this also consumes the remainder #
    movup.3
    u32add.unsafe
    movup.3
    movup.3
    u32addc
    eq.0
    assert

    movup.4             # make sure the result we got is equal to the dividend #
    assert.eq
    movup.3
    assert.eq           # quotient remains on the stack #
end
